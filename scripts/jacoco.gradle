/*
 *    Copyright 2020 Vasyl Rudas
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 *
 */

rootProject.ext.allJacocoTestReportFile = rootProject.file("$buildDir/reports/jacoco/allJacocoTestReport.xml")

allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    jacoco {
        toolVersion = "0.8.5"
    }

    tasks.jacocoTestReport {
        dependsOn(tasks.test)

        reports {
            xml.enabled true
        }
    }
}

rootProject.tasks.create(
    name: "allJacocoTestReport",
    type: JacocoReport.class,
    group: "verification"
) {
    dependsOn(allprojects.tasks.test)

    reports {
        xml.enabled true
        xml.setDestination(rootProject.allJacocoTestReportFile as File)
    }

    executionData(
        rootProject.fileTree(rootDir.getAbsolutePath())
            .include("**/build/jacoco/*.exec")
    )

    sourceSets(
        rootProject.subprojects
            .convention.collect { it.findPlugin(JavaPluginConvention.class) }
            .findAll { it != null }
            .collect { it.sourceSets.main }
            .findAll { it != null }
            as SourceSet[]
    )
}